apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "java-gradle-plugin"
apply plugin: "maven-publish"
apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.gradle.kotlin.kotlin-dsl"

group = "zbynek.firebase.test.lab"
version = "2.6.5"

pluginBundle {
    website = "https://github.com/zbynek/firebase-test-lab-gradle-plugin"
    vcsUrl = "https://github.com/zbynek/firebase-test-lab-gradle-plugin.git"
    tags = ["firebase", "test-lab", "espresso", "instrumental-tests", "kotlin", "android"]
}

gradlePlugin {
    plugins {
        FirebaseTestLabPlugin {
            id = "zbynek.firebase.test.lab"
            displayName = "Firebase Test Lab Plugin [FORKED]"
            description = "Gradle plugin for Android Test Lab"
            implementationClass = "com.appunite.firebasetestlabplugin.FirebaseTestLabPlugin"
        }
    }
}

jar {
    manifest {
        attributes(
                'Build-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ"),
                'Build-Revision': project.version,
                'Built-By': 'zbynek',
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
        )
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

tasks {
    test {
        testLogging.showExceptions = true
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'org.jetbrains.kotlin' && requested.name == 'kotlin-reflect') {
            details.useVersion kotlin_version
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.android.tools.build:gradle:3.6.1"
    testImplementation "junit:junit-dep:4.4"
}