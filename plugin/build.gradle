apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "java-gradle-plugin"
apply plugin: "maven"
apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.gradle.kotlin.kotlin-dsl"
apply plugin: "jacoco"

group = "firebase.test.lab"
version = "1.1.6"

gradlePlugin {
    plugins {
        FirebaseTestLabPlugin {
            id = "firebase-test-lab"
            implementationClass = "com.appunite.firebasetestlabplugin.FirebaseTestLabPlugin"
        }
    }
}

pluginBundle {
    website = "https://github.com/piotrmadry/firebase-test-lab-gradle-plugin"
    vcsUrl = "https://github.com/piotrmadry/firebase-test-lab-gradle-plugin.git"
    description = "Gradle plugin for Android Test Lab"
    tags = ["firebase", "test-lab", "espresso", "instrumental-tests", "kotlin", "android"]

    plugins {
        FirebaseTestLabPlugin {
            id = "firebase.test.lab"
            displayName = "Gradle Firebase Test Lab Plugin"
        }
    }
}

tasks {
    test {
        testLogging.showExceptions = true
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'org.jetbrains.kotlin' && requested.name == 'kotlin-reflect') {
            details.useVersion kotlin_version
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.android.tools.build:gradle:3.2.0"
    testCompile "junit:junit-dep:4.11"
}


jacocoTestReport {
    reports {
        html.enabled false
        csv.enabled false
        xml.enabled true
        xml.destination file("${rootProject.buildDir}/reports/jacoco/report.xml")
    }
}
